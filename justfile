set shell := ["bash", "-c"]

# images
SCYLLA_IMAGE := "scylladb/scylla"

# configurations
MANIFESTS := invocation_directory() / "test-resources/manifests"
DBSCHEMA := invocation_directory() / "test-resources/e2e"

# helm for Nexus
NEXUS_CHART_NAME := "nexus-supervisor"
NEXUS_CHART_PATH := "./.helm"
NEXUS_CHART_IMAGE_NAME := "nexus-supervisor-dev"
NEXUS_CHART_IMAGE_TAG  := "latest"
APP_VERSION := "0.0.0"
BUILD_NUMBER := "1"

# cluster
NEXUS_CLUSTER_NAME := "nexus-controller-0"

# Default recipe
fresh: stop up

# Start CI environment
up: start-kind-cluster install-ingress-controller create-namespace create-ingress scylla-kind build-image load-image dbschema deploy-chart

start-kind-cluster:
    kind create cluster --config=test-resources/kind.yaml --name {{NEXUS_CLUSTER_NAME}}

# Run all tests with coverage across both indexed and non-indexed store configurations
test-all:
    mkdir -p "$PWD/coverdir"
    @echo "==> Running tests with INDEXES_SUPPORTED=true"
    APPLICATION_ENVIRONMENT=units SCYLLA_STORE_LOCAL_ONLY=1 go test -v ./... -coverprofile="$PWD/coverdir/cover-indexed.out" -covermode=atomic -coverpkg=./...
    @echo "==> Switching to INDEXES_SUPPORTED=false"
    just switch-store-indexes "false"
    @echo "==> Running tests with INDEXES_SUPPORTED=false"
    APPLICATION_ENVIRONMENT=units SCYLLA_STORE_LOCAL_ONLY=1 go test -v ./... -coverprofile="$PWD/coverdir/cover-bare.out" -covermode=atomic -coverpkg=./...
    @echo "==> Merging coverage profiles into cover.out"
    go run github.com/wadey/gocovmerge@latest "$PWD/coverdir/cover-indexed.out" "$PWD/coverdir/cover-bare.out" > "$PWD/cover.out"

# Cleanup CI environment
stop:
    @echo "🧹 Cleaning up..."
    kind delete cluster --name {{NEXUS_CLUSTER_NAME}}
    rm -f cover-indexed.out cover-bare.out cover.out

# View logs
logs name="":
    docker logs -f {{if name == "" { "scylla" } else { name }}}

# build the local Docker image
build-image:
    docker build \
        --build-arg APPVERSION={{APP_VERSION}} \
        --build-arg BUILDNUMBER={{BUILD_NUMBER}} \
        -t {{NEXUS_CHART_IMAGE_NAME}}:{{NEXUS_CHART_IMAGE_TAG}} \
        -f .container/Dockerfile .

# load image into the cluster
load-image:
    kind load docker-image {{NEXUS_CHART_IMAGE_NAME}}:{{NEXUS_CHART_IMAGE_TAG}} --name  {{NEXUS_CLUSTER_NAME}}

create-namespace:
    kubectl create namespace nexus --dry-run=client -o yaml | kubectl apply -f -

# install chart
deploy-chart indexes="true":
    kubectl create secret generic cassandra-credentials \
        --namespace nexus \
        --from-literal=NEXUS__SCYLLA_CQL_STORE__HOSTS="scylla.nexus.svc.cluster.local" \
        --from-literal=NEXUS__SCYLLA_CQL_STORE__INDEXES_SUPPORTED="{{indexes}}" \
        --from-literal=NEXUS__SCYLLA_CQL_STORE__USER="cassandra" \
        --from-literal=NEXUS__SCYLLA_CQL_STORE__PASSWORD="cassandra" \
        --from-literal=NEXUS__SCYLLA_CQL_STORE__KEYSPACE="nexus" --dry-run=client -o yaml | kubectl apply -f -

    helm upgrade --install --create-namespace --namespace nexus {{NEXUS_CHART_NAME}} {{NEXUS_CHART_PATH}} \
        --set image.repository={{NEXUS_CHART_IMAGE_NAME}} \
        --set image.tag={{NEXUS_CHART_IMAGE_TAG}} \
        --set image.pullPolicy=Never \
        --set supervisor.config.checkpointStore.type=cassandra-scylla \
        --set supervisor.config.checkpointStore.secretName="cassandra-credentials"

# switch Cassandra store index mode and rollout deployment
switch-store-indexes indexes="false":
    kubectl create secret generic cassandra-credentials \
        --namespace nexus \
        --from-literal=NEXUS__SCYLLA_CQL_STORE__HOSTS="scylla.nexus.svc.cluster.local" \
        --from-literal=NEXUS__SCYLLA_CQL_STORE__INDEXES_SUPPORTED="{{indexes}}" \
        --from-literal=NEXUS__SCYLLA_CQL_STORE__USER="cassandra" \
        --from-literal=NEXUS__SCYLLA_CQL_STORE__PASSWORD="cassandra" \
        --from-literal=NEXUS__SCYLLA_CQL_STORE__KEYSPACE="nexus" --dry-run=client -o yaml | kubectl apply -f -
    kubectl rollout restart deployment/{{NEXUS_CHART_NAME}} -n nexus
    kubectl rollout status deployment/{{NEXUS_CHART_NAME}} -n nexus --timeout=120s

# cleanup
remove-chart:
    helm uninstall -n nexus {{NEXUS_CHART_NAME}}

install-ingress-controller:
    kubectl apply -f https://kind.sigs.k8s.io/examples/ingress/deploy-ingress-nginx.yaml
    kubectl rollout status deployment/ingress-nginx-controller -n ingress-nginx --timeout=180s

create-ingress:
    # Create ingress rules for services
    for i in $(seq 1 30); do \
      kubectl apply -f {{MANIFESTS}}/ingress.yaml && break || \
      (echo "Retry $i/30: failed to apply ingress, retrying in 1s..." && sleep 1); \
    done; \
    if [ $i -eq 30 ]; then \
      echo "Failed to apply ingress after 30 attempts."; \
      exit 1; \
    fi

scylla-kind:
    kubectl apply -f {{MANIFESTS}}/scylladb.yaml
    kubectl -n nexus rollout status deployment/scylla --timeout=180s

dbschema:
  docker run --rm -v {{DBSCHEMA}}:/opt/storage --network=host --entrypoint /opt/storage/prepare-db.sh {{SCYLLA_IMAGE}}
